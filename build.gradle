apply plugin: "java"
apply plugin: "ivy-publish"

group="grl"
version="3.4"
status="release"

repositories {
	mavenCentral()
	ivy {
		// URL can refer to a local directory
		url "${System.properties['user.home']}/.ivy2/local"
	}
}

configurations {
	compile {
		transitive = true
	}
}

dependencies {
	compile(ivyDependencies('compile'))
}

artifacts {
	archives jar
}

publishing{
	publications {
		ivyJava(IvyPublication) {
			from components.java
		}
	}
	repositories {
		ivy {
			url "${System.properties['user.home']}/.ivy2/local"
		}
	}
}

def ivyDependencies(conf) {
	def dep = []
	def ivyModule = new XmlParser().parse(file("ivy.xml"))
   
	ivyModule.dependencies.dependency.each {
	  if(it.@conf == null || it.@conf == conf)
		dep.add(it.@org + ':' + it.@name + ':' + it.@rev)
	}
   
	return dep
}

